plugins {
	id 'java'
	id 'com.vaadin' version '24.5.1'
	id 'org.cyclonedx.bom' version '1.10.0'
	id 'com.gorylenko.gradle-git-properties' version '2.4.2'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'org.cftoolsuite'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://maven.vaadin.com/vaadin-addons/' }
}

ext {
	set('springCloudVersion', "2023.0.3")
	set('vaadinVersion', "24.5.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'io.github.openfeign:feign-hc5:13.5'
	implementation 'com.vaadin:vaadin-spring-boot-starter'
	implementation 'org.vaadin.olli:file-download-wrapper:7.1.0'
	implementation 'org.commonmark:commonmark:0.24.0'
	implementation 'org.apache.commons:commons-lang3'
	implementation 'io.pivotal.cfenv:java-cfenv-all:3.2.0'
	implementation 'org.springframework.cloud:spring-cloud-bindings:2.0.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

bootRun {
    systemProperties System.properties
}

task setVersion {
    doLast {
        def newVersion = project.hasProperty('newVersion') ? project.newVersion : version
        ant.propertyfile(file: "gradle.properties") {
            entry(key: "version", value: newVersion)
        }
    }
}

task printArtifactPath {
    doLast {
        println bootJar.archiveFile.get().asFile.absolutePath
    }
}
