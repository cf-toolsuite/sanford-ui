plugins {
	id 'java'
	id 'com.vaadin' version '24.8.8'
	id 'org.cyclonedx.bom' version '2.3.1'
	id 'com.gorylenko.gradle-git-properties' version '2.5.2'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.eclipse.jkube.kubernetes' version '1.18.1'
}

group = 'org.cftoolsuite'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://maven.vaadin.com/vaadin-addons/' }
}

ext {
	set('springCloudVersion', "2025.0.0")
	set('vaadinVersion', "24.8.2")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation 'org.apache.commons:commons-collections4:4.5.0'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.2.2'
	implementation 'io.github.openfeign:feign-hc5:13.6'
	implementation 'com.playtika.reactivefeign:feign-reactor-spring-cloud-starter:4.2.1'
	implementation 'com.vaadin:vaadin-spring-boot-starter'
	implementation 'org.vaadin.olli:file-download-wrapper:7.1.0'
	implementation 'org.apache.commons:commons-lang3'
	implementation 'io.pivotal.cfenv:java-cfenv-all:3.5.0'
	implementation 'org.springframework.cloud:spring-cloud-bindings:2.0.4'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

bootRun {
    systemProperties System.properties
}

tasks.named('bootBuildImage') {
	imageName = "docker.io/${project.group.contains('.') ? project.group.tokenize('.').last() : project.group}/${project.name}:${project.version}"
	tags = [ "docker.io/${project.group.contains('.') ? project.group.tokenize('.').last() : project.group}/${project.name}:latest" ]
}

kubernetes {
	buildStrategy = 'buildpacks'
    generator {
        config {
            'spring-boot' {
                color = 'always'
            }
        }
    }
    helm {
        maintainers = [{
            name = 'Chris Phillipson'
        }]
    }
}

tasks.register('setVersion') {
	doLast {
		def newVersion = project.hasProperty('newVersion') ? project.newVersion : version
		ant.propertyfile(file: "gradle.properties") {
			entry(key: "version", value: newVersion)
		}
	}
}

tasks.register('printArtifactPath') {
	doLast {
		println bootJar.archiveFile.get().asFile.absolutePath
	}
}
